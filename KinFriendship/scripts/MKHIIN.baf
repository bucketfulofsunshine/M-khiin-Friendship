// Timer-Based Talks
IF
    InParty(Myself)
    Global("BFSKinFriendshipActive","GLOBAL",0)
THEN
  RESPONSE #100
    RealSetGlobalTimer("BFSKinFriendshipTalksTimer","GLOBAL",1200)
    SetGlobal("BFSKinFriendshipTalks","GLOBAL",1)
    SetGlobal("BFSKinFriendshipActive","GLOBAL",1)
END

IF
    InParty(Myself)
    See(Player1)
    RealGlobalTimerExpired("BFSKinFriendshipTalksTimer","GLOBAL")
	!StateCheck(Player1,CD_STATE_NOTVALID)    // CamDawg's conditions for no charm etc
    !StateCheck(Myself,CD_STATE_NOTVALID)
    CombatCounter(0)                          // Not in battle please
    !See([ENEMY])
    Global("BFSKinFriendshipActive","GLOBAL",1)
    OR(5)
      Global("BFSKinFriendshipTalks","GLOBAL",1)
      Global("BFSKinFriendshipTalks","GLOBAL",3)
      Global("BFSKinFriendshipTalks","GLOBAL",5)
      Global("BFSKinFriendshipTalks","GLOBAL",7)
	  Global("BFSKinFriendshipTalks","GLOBAL",9)
THEN
  RESPONSE #100
    IncrementGlobal("BFSKinFriendshipTalks","GLOBAL",1)
END

IF
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    Global("BFSKinFriendshipActive","GLOBAL",1)
    CombatCounter(0)
    !See([ENEMY])
    OR(5)
      Global("BFSKinFriendshipTalks","GLOBAL",2)
      Global("BFSKinFriendshipTalks","GLOBAL",4)
      Global("BFSKinFriendshipTalks","GLOBAL",6)
      Global("BFSKinFriendshipTalks","GLOBAL",8)
      Global("BFSKinFriendshipTalks","GLOBAL",10)
THEN
  RESPONSE #100
    MoveToObject(Player1)
    Dialogue(Player1)
END

// Dragon Cave Warning
IF
InParty(Myself)
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
AreaCheck("BD7210")           
Global("BFSMkDragon","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("BFSMkDragon","GLOBAL",1)
END

IF
  Global("BFSMkDragon","GLOBAL",1)
 THEN
 RESPONSE #100
  MoveToObject(Player1)
  Dialogue(Player1)
END 

// Dragon Dead
IF
InParty(Myself)
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
Dead("BDMORENT")          
Global("BFSMkDragon","GLOBAL",2)
THEN
RESPONSE #100
SetGlobal("BFSMkDragon","GLOBAL",3)
END

IF
  Global("BFSMkDragon","GLOBAL",3)
 THEN
 RESPONSE #100
  MoveToObject(Player1)
  Dialogue(Player1)
END 

// Boareskyr bridge fallout
// THANK YOU JASTEY! <3 
/* activate - no The Boareskyr Bridge Scene mod installed */
IF
	InParty("MKHIIN")
	// !RealGlobalTimerNotExpired("BFSMkDialogueTimer","GLOBAL")
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	GlobalGT("bd_plot","global",294) //after it happened
	GlobalLT("BFSMkBoareskyr","GLOBAL",3)
THEN
	RESPONSE #100
		SetGlobal("BFSMkBoareskyr","GLOBAL",3)
END
/* initiate */
IF
	InParty("MKHIIN")
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	Global("BFSMkBoareskyr","GLOBAL",3)
THEN
	RESPONSE #100
		// RealSetGlobalTimer("BFSMkDialogTimer","GLOBAL",600)
		StartDialogNoSet(Player1)
END

